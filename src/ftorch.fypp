#:def ranksuffix(RANK)
$:'' if RANK == 0 else '(' + ':' + ',:' * (RANK - 1) + ')'
#:enddef ranksuffix
#:set PRECISIONS = ['int8', 'int16', 'int32', 'int64', 'real32', 'real64']
#:set C_PRECISIONS = ['c_int8_t', 'c_int16_t', 'c_int32_t', 'c_int64_t', 'c_float', 'c_double']
#:set C_PRECISIONS = dict(zip(PRECISIONS, C_PRECISIONS))
#:set ENUMS = dict(zip(PRECISIONS, ['torch_kInt8', 'torch_kInt16', 'torch_kInt32', 'torch_kInt64', 'torch_kFloat32', 'torch_kFloat64']))
#:set RANKS = range(1, 5)
#:def enum_from_prec(PRECISION)
$:ENUMS[PRECISION]
#:enddef enum_from_prec
#:def c_prec(PRECISION)
$:C_PRECISIONS[PRECISION]
#:enddef c_prec
#:def f_type(PRECISION)
$:'integer' if PRECISION[:3] == 'int' else 'real'
#:enddef f_type
!| Main module for FTorch containing types and procedures.
!  Generated from `ftorch.fypp` using the [fypp Fortran preprocessor](https://fypp.readthedocs.io/en/stable/index.html).
!
!  * License  
!    FTorch is released under an MIT license.
!    See the [LICENSE](https://github.com/Cambridge-ICCS/FTorch/blob/main/LICENSE)
!    file for details.

module ftorch

  use, intrinsic :: iso_c_binding, only: c_int, c_int8_t, c_int16_t, c_int32_t, c_int64_t, c_int64_t, &
                                         c_float, c_double, c_char, c_ptr, c_null_ptr
  use, intrinsic :: iso_fortran_env, only: int8, int16, int32, int64, real32, real64

  implicit none

  !> Type for holding a torch neural net (nn.Module).
  type torch_module
    type(c_ptr) :: p = c_null_ptr  !! pointer to the neural net module in memory
  end type torch_module

  !> Type for holding a Torch tensor.
  type torch_tensor
    type(c_ptr) :: p = c_null_ptr  !! pointer to the tensor in memory
  end type torch_tensor

  !| Enumerator for Torch data types  
  !  From c_torch.h (torch_data_t)  
  !  Note that 0 `torch_kUInt8` and 5 `torch_kFloat16` are not sypported in Fortran
  enum, bind(c)
    enumerator :: torch_kUInt8 = 0 ! not supported in Fortran
    enumerator :: torch_kInt8 = 1
    enumerator :: torch_kInt16 = 2
    enumerator :: torch_kInt32 = 3
    enumerator :: torch_kInt64 = 4
    enumerator :: torch_kFloat16 = 5 ! not supported in Fortran
    enumerator :: torch_kFloat32 = 6
    enumerator :: torch_kFloat64 = 7
  end enum


  !| Enumerator for Torch devices  
  !  From c_torch.h (torch_device_t)
  enum, bind(c)
    enumerator :: torch_kCPU = 0
    enumerator :: torch_kCUDA = 1
  end enum

  !> Interface for directing `torch_tensor_from_array` to possible input types and ranks
  interface torch_tensor_from_array
    #:for PREC in PRECISIONS
    #:for RANK in RANKS
    module procedure torch_tensor_from_array_${PREC}$_${RANK}$d
    #:endfor
    #:endfor
  end interface

  interface
    function torch_from_blob_c(data, ndims, tensor_shape, strides, dtype, device, device_number) result(tensor_p) &
                               bind(c, name = 'torch_from_blob')
      use, intrinsic :: iso_c_binding, only : c_int, c_int64_t, c_ptr

      ! Arguments
      type(c_ptr), value, intent(in)    :: data
      integer(c_int), value, intent(in) :: ndims
      integer(c_int64_t), intent(in)    :: tensor_shape(*)
      integer(c_int64_t), intent(in)    :: strides(*)
      integer(c_int), value, intent(in) :: dtype
      integer(c_int), value, intent(in) :: device
      integer(c_int), value, intent(in) :: device_number
      type(c_ptr)                       :: tensor_p
    end function torch_from_blob_c
  end interface

contains

  !> Returns a tensor filled with the scalar value 0.
  function torch_tensor_zeros(ndims, tensor_shape, dtype, device, device_number) result(tensor)
    use, intrinsic :: iso_c_binding, only : c_int, c_int64_t
    integer(c_int), intent(in)     :: ndims      !! Number of dimensions of the tensor
    integer(c_int64_t), intent(in) :: tensor_shape(*)   !! Shape of the tensor
    integer(c_int), intent(in)     :: dtype      !! Data type of the tensor
    integer(c_int), intent(in)     :: device     !! Device on which the tensor will live on (`torch_kCPU` or `torch_kCUDA`)
    integer(c_int), optional, intent(in) :: device_number     !! Device number to use for `torch_kCUDA` case
    type(torch_tensor)             :: tensor     !! Returned tensor
    integer(c_int)                 :: device_number_value  !! Device number used

    interface
      function torch_zeros_c(ndims, tensor_shape, dtype, device, device_number) result(tensor) &
          bind(c, name = 'torch_zeros')
        use, intrinsic :: iso_c_binding, only : c_int, c_int64_t, c_ptr
        integer(c_int), value, intent(in) :: ndims
        integer(c_int64_t), intent(in)    :: tensor_shape(*)
        integer(c_int), value, intent(in) :: dtype
        integer(c_int), value, intent(in) :: device
        integer(c_int), value, intent(in) :: device_number
        type(c_ptr)                       :: tensor
      end function torch_zeros_c
    end interface

    ! Process optional arguments
    if (present(device_number)) then
      device_number_value = device_number
    else
      device_number_value = 0
    endif

    tensor%p = torch_zeros_c(ndims, tensor_shape, dtype, device, device_number_value)
  end function torch_tensor_zeros

  !> Returns a tensor filled with the scalar value 1.
  function torch_tensor_ones(ndims, tensor_shape, dtype, device, device_number) result(tensor)
    use, intrinsic :: iso_c_binding, only : c_int, c_int64_t
    integer(c_int), intent(in)     :: ndims      !! Number of dimensions of the tensor
    integer(c_int64_t), intent(in) :: tensor_shape(*)   !! Shape of the tensor
    integer(c_int), intent(in)     :: dtype      !! Data type of the tensor
    integer(c_int), intent(in)     :: device     !! Device on which the tensor will live on (`torch_kCPU` or `torch_kCUDA`)
    integer(c_int), optional, intent(in) :: device_number     !! Device number to use for `torch_kCUDA` case
    type(torch_tensor)             :: tensor     !! Returned tensor
    integer(c_int)                 :: device_number_value  !! Device number used

    interface
      function torch_ones_c(ndims, tensor_shape, dtype, device, device_number) result(tensor) &
          bind(c, name = 'torch_ones')
        use, intrinsic :: iso_c_binding, only : c_int, c_int64_t, c_ptr
        integer(c_int), value, intent(in) :: ndims
        integer(c_int64_t), intent(in)    :: tensor_shape(*)
        integer(c_int), value, intent(in) :: dtype
        integer(c_int), value, intent(in) :: device
        integer(c_int), value, intent(in) :: device_number
        type(c_ptr)                       :: tensor
      end function torch_ones_c
    end interface

    ! Process optional arguments
    if (present(device_number)) then
      device_number_value = device_number
    else
      device_number_value = 0
    endif

    tensor%p = torch_ones_c(ndims, tensor_shape, dtype, device, device_number_value)
  end function torch_tensor_ones

  ! Torch Tensor API
  !| Exposes the given data as a tensor without taking ownership of the original data.
  !  This routine will take an (i, j, k) array and return an (k, j, i) tensor.
  function torch_tensor_from_blob(data, ndims, tensor_shape, layout, dtype, device, device_number) result(tensor)
    use, intrinsic :: iso_c_binding, only : c_int, c_int64_t, c_ptr
    type(c_ptr), intent(in)        :: data       !! Pointer to data
    integer(c_int), intent(in)     :: ndims      !! Number of dimensions of the tensor
    integer(c_int64_t), intent(in) :: tensor_shape(*)   !! Shape of the tensor
    integer(c_int), intent(in)     :: dtype      !! Data type of the tensor
    integer(c_int), intent(in)     :: device     !! Device on which the tensor will live on (`torch_kCPU` or `torch_kCUDA`)
    integer(c_int), optional, intent(in) :: device_number     !! Device number to use for `torch_kCUDA` case
    integer(c_int), intent(in)     :: layout(*)  !! Layout for strides for accessing data
    type(torch_tensor)             :: tensor     !! Returned tensor

    integer(c_int)                 :: i          !! loop index
    integer(c_int64_t)             :: strides(ndims) !! Strides for accessing data
    integer(c_int)                 :: device_number_value  !! Device number used

    strides(layout(1)) = 1
    do i = 2, ndims
      strides(layout(i)) = strides(layout(i - 1)) * tensor_shape(layout(i - 1))
    end do

    ! Process optional arguments
    if (present(device_number)) then
      device_number_value = device_number
    else
      device_number_value = 0
    endif

    tensor%p = torch_from_blob_c(data, ndims, tensor_shape, strides, dtype, device, device_number_value)
  end function torch_tensor_from_blob

  !> Prints the contents of a tensor.
  subroutine torch_tensor_print(tensor)
    type(torch_tensor), intent(in) :: tensor  !! Input tensor

    interface
      subroutine torch_tensor_print_c(tensor) &
          bind(c, name = 'torch_tensor_print')
        use, intrinsic :: iso_c_binding, only : c_ptr
        type(c_ptr), value, intent(in) :: tensor
      end subroutine torch_tensor_print_c
    end interface

    call torch_tensor_print_c(tensor%p)
  end subroutine torch_tensor_print

  !> Deallocates a tensor.
  subroutine torch_tensor_delete(tensor)
    type(torch_tensor), intent(in) :: tensor     !! Input tensor

    interface
      subroutine torch_tensor_delete_c(tensor) &
          bind(c, name = 'torch_tensor_delete')
        use, intrinsic :: iso_c_binding, only : c_ptr
        type(c_ptr), value, intent(in) :: tensor
      end subroutine torch_tensor_delete_c
    end interface

    call torch_tensor_delete_c(tensor%p)
  end subroutine torch_tensor_delete

  ! Torch Module API
  !> Loads a TorchScript module (pre-trained PyTorch model saved with TorchScript)
  function torch_module_load(filename, device, device_number) result(module)
    use, intrinsic :: iso_c_binding, only : c_int, c_null_char
    character(*), intent(in)   :: filename !! Filename of TorchScript module
    integer(c_int), optional, intent(in) :: device !! Device on which the tensor will live on (`torch_kCPU` or `torch_kCUDA`)
    integer(c_int), optional, intent(in) :: device_number !! Device number to use for `torch_kCUDA` case
    type(torch_module)         :: module   !! Returned deserialized module
    integer(c_int) :: device_value
    integer(c_int) :: device_number_value

    interface
      function torch_jit_load_c(filename, device, device_number) result(module) &
          bind(c, name = 'torch_jit_load')
        use, intrinsic :: iso_c_binding, only : c_char, c_int, c_ptr
        character(c_char), intent(in) :: filename(*)
        integer(c_int), value, intent(in)    :: device
        integer(c_int), value, intent(in)    :: device_number
        type(c_ptr)                   :: module
      end function torch_jit_load_c
    end interface

    ! Process optional arguments
    if (present(device)) then
      device_value = device
    else
      device_value = 0
    endif
    if (present(device_number)) then
      device_number_value = device_number
    else
      device_number_value = 0
    endif

    ! Need to append c_null_char at end of filename
    module%p = torch_jit_load_c(trim(adjustl(filename))//c_null_char, device_value, device_number_value)
  end function torch_module_load

  !> Performs a forward pass of the module with the input tensors
  subroutine torch_module_forward(module, input_tensors, n_inputs, output_tensor)
    use, intrinsic :: iso_c_binding, only : c_ptr, c_int, c_loc
    type(torch_module), intent(in) :: module        !! Module
    type(torch_tensor), intent(in), dimension(:) :: input_tensors  !! Array of Input tensors
    type(torch_tensor), intent(in) :: output_tensor !! Returned output tensors
    integer(c_int) ::  n_inputs

    integer :: i
    type(c_ptr), dimension(n_inputs), target  :: input_ptrs

    interface
      subroutine torch_jit_module_forward_c(module, input_tensors, n_inputs, &
          output_tensor) &
          bind(c, name = 'torch_jit_module_forward')
        use, intrinsic :: iso_c_binding, only : c_ptr, c_int
        type(c_ptr), value, intent(in) :: module
        type(c_ptr), value, intent(in) :: input_tensors
        integer(c_int), value, intent(in) :: n_inputs
        type(c_ptr), value, intent(in) :: output_tensor
      end subroutine torch_jit_module_forward_c
    end interface

    ! Assign array of pointers to the input tensors
    do i = 1, n_inputs
      input_ptrs(i) = input_tensors(i)%p
    end do

    call torch_jit_module_forward_c(module%p, c_loc(input_ptrs), n_inputs, output_tensor%p)
  end subroutine torch_module_forward

  !> Deallocates a TorchScript module
  subroutine torch_module_delete(module)
    type(torch_module), intent(in) :: module     !! Module to deallocate

    interface
      subroutine torch_jit_module_delete_c(module) &
          bind(c, name = 'torch_jit_module_delete')
        use, intrinsic :: iso_c_binding, only : c_ptr
        type(c_ptr), value, intent(in) :: module
      end subroutine torch_jit_module_delete_c
    end interface

    call torch_jit_module_delete_c(module%p)
  end subroutine torch_module_delete

  #:for PREC in PRECISIONS
  #:for RANK in RANKS
  !> Return a Torch tensor pointing to data_in array of rank ${RANK}$ containing data of type `${PREC}$`
  function torch_tensor_from_array_${PREC}$_${RANK}$d(data_in, layout, c_device, device_number) result(tensor)
    use, intrinsic :: iso_c_binding, only : c_int, c_int64_t, c_float, c_loc
    use, intrinsic :: iso_fortran_env, only : ${PREC}$

    ! inputs
    ${f_type(PREC)}$(kind=${PREC}$), intent(in), target :: data_in${ranksuffix(RANK)}$   !! Input data that tensor will point at
    integer, intent(in)        :: layout(${RANK}$) !! Control order of indices
    integer(c_int), intent(in) :: c_device         !! Device on which the tensor will live on (`torch_kCPU` or `torch_kCUDA`)
    integer(c_int), optional, intent(in) :: device_number    !! Device number to use for `torch_kCUDA` case

    ! output tensory
    type(torch_tensor) :: tensor     !! Returned tensor

    ! local data
    integer(c_int64_t)        :: c_tensor_shape(${RANK}$)           !! Shape of the tensor
    integer(c_int), parameter :: c_dtype = ${enum_from_prec(PREC)}$ !! Data type
    integer(c_int64_t)        :: strides(${RANK}$)                  !! Strides for accessing data
    integer(c_int), parameter :: ndims = ${RANK}$                   !! Number of dimension of input data
    integer                   :: i
    integer(c_int)            :: device_number_value

    c_tensor_shape = shape(data_in)

    strides(layout(1)) = 1
    do i = 2, ndims
      strides(layout(i)) = strides(layout(i - 1)) * c_tensor_shape(layout(i - 1))
    end do

    ! Process optional arguments
    if (present(device_number)) then
      device_number_value = device_number
    else
      device_number_value = 0
    endif

    tensor%p = torch_from_blob_c(c_loc(data_in), ndims, c_tensor_shape, strides, c_dtype, c_device, device_number_value)

  end function torch_tensor_from_array_${PREC}$_${RANK}$d

  #:endfor
  #:endfor

end module ftorch
