!| Unit tests for FTorch subroutines that interrogate tensors on a CUDA device.
!
!  * License
!    FTorch is released under an MIT license.
!    See the [LICENSE](https://github.com/Cambridge-ICCS/FTorch/blob/main/LICENSE)
!    file for details.
module unittest_interrogation_cuda
  use funit
  use ftorch_devices, only: torch_kCUDA
  use ftorch_types, only: torch_kFloat32
  use ftorch_tensor, only: torch_tensor, torch_tensor_empty
  use iso_c_binding, only: c_int64_t

  implicit none

  public

  ! All unit tests in this module run on CUDA GPU devices
  integer, parameter :: device_type = torch_kCUDA

  ! All unit tests in this module use float32 precision
  integer, parameter :: dtype = torch_kFloat32

contains

  ! Unit test for the torch_tensor_get_device_type function applied to a tensor on a CUDA device
  @test
  subroutine test_get_device_type()

    implicit none

    type(torch_tensor) :: tensor
    integer, parameter :: ndims = 1
    integer(c_int64_t), parameter, dimension(ndims) :: tensor_shape = [1]
    integer, parameter :: expected = torch_kCUDA

    ! Create an empty tensor on the CUDA device and check that torch_tensor_get_device_type can get
    ! the device type
    call torch_tensor_empty(tensor, ndims, tensor_shape, dtype, device_type)
    @assertEqual(expected, tensor%get_device_type())

  end subroutine test_get_device_type

  ! Unit test for the torch_tensor_get_device_index function applied to a tensor on a CUDA device
  ! Tensor is created without specifying a device_index so should default to device 0
  @test
  subroutine test_get_device_index_default()

    implicit none

    type(torch_tensor) :: tensor
    integer, parameter :: ndims = 1
    integer(c_int64_t), parameter, dimension(ndims) :: tensor_shape = [1]
    integer, parameter :: expected = 0

    ! Create an empty tensor on the CUDA device with the default device index and check that
    ! torch_tensor_get_device_index can get the device index
    call torch_tensor_empty(tensor, ndims, tensor_shape, dtype, device_type)
    @assertEqual(expected, tensor%get_device_index())

  end subroutine test_get_device_index_default

end module unittest_interrogation_cuda
